#!/opt/kronometrix/perl/bin/perl

#  Copyright (c) 2009-2022 Stefan Parvu (gitlab.com/sparvu)
#  Initial Author: Stefan Parvu
#
#  This program is free software; you can redistribute it and/or
#  modify it under the terms of the GNU General Public License
#  as published by the Free Software Foundation; either version 2
#  of the License, or (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program; if not, write to the Free Software Foundation,
#  Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
#
#  (http://www.gnu.org/copyleft/gpl.html)

use strict;
use warnings;
use JSON;
use Getopt::Std;
use Time::HiRes qw(time alarm setitimer ITIMER_REAL);
use Linux::Info;
use POSIX qw(pause);

###  Process command line args
usage() if defined $ARGV[0] and $ARGV[0] eq "--help";
getopts('lhV') or usage();
usage()    if defined $main::opt_h;
revision() if defined $main::opt_V;

# logging flag
my $logging = defined $main::opt_l ? $main::opt_l : 0;

# process [interval [count]],
my ( $interval, $loop_max );
if ( defined $ARGV[0] ) {
    $interval = $ARGV[0];
    $loop_max = defined $ARGV[1] ? $ARGV[1] : 2**32;
    usage() if $interval == 0;
}
else {
    $interval = 1;
    $loop_max = 1;
}

###  Variables
my $sb         = "/sys/block";       # sys block dir
my $loop       = 0;                  # current loop number
my $tp         = 0;                  # time precision
$main::opt_h   = 0;                  # help option
$main::opt_l   = 0;                  # logging option
$main::opt_V   = 0;                  # revision option
my $kfile      = 'kronometrix.json'; # configuration file
my $kdata;                           # configuration data
my ($baselog, $curlog, $slog, $log);
local $| = 1;


### MAIN BODY

# Set a timer for L::I object
local $SIG{ALRM} = sub { };
setitimer( ITIMER_REAL, .1, .1 );
my $lxs = Linux::Info->new(
    cpustats  => 1,
    memstats  => 1,
    diskstats => 1,
    netstats  => 1,
    loadavg   => 1
);
### 0.1sec sleep using a timer
pause;

# how often do we trigger (seconds)?
my $first_interval = $interval;

# signal handler is empty
local $SIG{ALRM} = sub { };

# first value is the initial wait, second is the wait thereafter
setitimer( ITIMER_REAL, $first_interval, $interval );

if ( $interval =~ /\./ ) {
    $tp = 3;
}

my $rawfile;

# logging option
if ($logging) {

    # kronometrix.json config file
    $kdata = open_config($kfile);

    ## logs
    ( $baselog, $curlog ) = get_log($kdata);
    $rawfile = $curlog . '/' . 'sysrec' . '.krd';
    $slog = $baselog . "/sysrec.log";
    $log = open_file($slog);

    # save STDERR to log file
    *STDERR = $log;

    write_log ("info: started");
}

while (1) {

    my $reads = 0;
    my $rkbytes = 0;
    my $writes = 0;
    my $wkbytes = 0;
    my $iops = 0;
    my $rwkbytes = 0;

    # net stats
    my $rxpkt = 0;
    my $rxkbytes = 0;
    my $rxerr = 0;
    my $rxdrop = 0;
    my $txpkt = 0;
    my $txkbytes = 0;
    my $txerr = 0;
    my $txdrop = 0;
    my $ntByt = 0;
    my $rxtxpkt = 0;
    my $rxtxkbytes = 0;

    # runq size and task count
    my $rqsz = 0;
    my $tcount = 0;

    ### Get Stats
    my $stat    = $lxs->get;

    # Dump $lx variable
    # use Data::Dumper;
    # print Dumper($stat);

    my $cpu     = $stat->cpustats->{cpu};
    my $mem     = $stat->memstats;
    my $disk    = $stat->diskstats;
    my $nic     = $stat->netstats;
    my $la      = $stat->loadavg;

    ### sum(cpus)
    my $scpus = $stat->cpustats;
    my @cpus = sort $stat->cpustats;
    my $ncpu = 0;
    my $numcpus = 0;

    foreach (@cpus) {
        next if ( $_ eq "cpu" );
        $ncpu += $scpus->{$_}{total};
        $numcpus++;
    }

    ### headroom cpu
    my $hcpu = ($numcpus * 100) - $ncpu;


    ### get disks
    my $rdisks = get_disks();

    ### loop over all disks
    foreach (@$rdisks) {

        # print "\nDisk: $_ \n";

        if ( defined( $disk->{$_}{rdreq} ) ) {
            $reads += $disk->{$_}{rdreq};
        }

        if ( defined( $disk->{$_}{rdbyt} ) ) {
            $rkbytes += ( $disk->{$_}{rdbyt} / 1024 );
        }

        if ( defined( $disk->{$_}{wrtreq} ) ) {
            $writes += $disk->{$_}{wrtreq};
        }

        if ( defined( $disk->{$_}{wrtbyt} ) ) {
            $wkbytes += ( $disk->{$_}{wrtbyt} / 1024 );
        }

        if ( defined( $disk->{$_}{ttreq} ) ) {
            $iops += $disk->{$_}{ttreq};
        }

        if ( defined( $disk->{$_}{ttbyt} ) ) {
            $rwkbytes += ( $disk->{$_}{ttbyt} / 1024 );
        }

        ### Debug
        #print "readReq = $readReq \n";
        #print "writeReq = $writeReq \n";
        #print "totReq = $totReq \n";
        #print "readBytes = $readByt\n";
        #print "writeReq = $writeByt \n";
        #print "totByt = $totByt \n";
    }

    ### Get NICS
    my @nics = sort $stat->netstats;

    # loop over all NICS
    foreach (@nics) {

        # skip here the loopback interface
        next if $_ =~ /^lo.*/;
        next if $_ =~ /^vif.*/;
        next if $_ =~ /^macvtap.*/;

        #
        # print "NIC: $_ \n";

        # rx
        if ( defined( $nic->{$_}{rxpcks} ) ) {
            $rxpkt += ( $nic->{$_}{rxpcks} );
        }

        if ( defined( $nic->{$_}{rxbyt} ) ) {
            $rxkbytes += ( $nic->{$_}{rxbyt} / 1024 );
        }

        if ( defined( $nic->{$_}{rxerrs} ) ) {
            $rxerr += ( $nic->{$_}{rxerrs} );
        }

        if ( defined( $nic->{$_}{rxdrop} ) ) {
            $rxdrop += ( $nic->{$_}{rxdrop} );
        }

        # tx
        if ( defined( $nic->{$_}{txpcks} ) ) {
            $txpkt += ( $nic->{$_}{txpcks} );
        }

        if ( defined( $nic->{$_}{txbyt} ) ) {
            $txkbytes += ( $nic->{$_}{txbyt} / 1024 );
        }

        if ( defined( $nic->{$_}{txerrs} ) ) {
            $txerr += ( $nic->{$_}{txerrs} );
        }

        if ( defined( $nic->{$_}{txdrop} ) ) {
            $txdrop += ( $nic->{$_}{txdrop} );
        }
 
        # throughput
        if ( defined( $nic->{$_}{ttbyt} ) ) {
            $rxtxkbytes += ( $nic->{$_}{ttbyt} / 1024 );
        }

    }

    $rxtxpkt = $rxpkt + $txpkt;
    # $rxtxkbytes = $rxkbytes + $txkbytes;


    # get runq_sz and process count from /proc/loadavg
    open my $plavg, '<', '/proc/loadavg'
      or die "Error: Cannot find proc loadavg file: $!\n";

    ($rqsz, $tcount) = (split m@/@, (split /\s+/, <$plavg>)[3]);

    close $plavg;

    # exclude from runqa_sz current process
    $rqsz--;

    if ($logging) {
    
        my $rawkrd = open_file($rawfile);

        printf $rawkrd
        "%.${tp}f:%.2f:%.2f:%.2f:%.2f:%.2f:%.2f:%.2f:%.2f:%.2f:%.2f:%.2f:%d:%d:%.2f:%d:%d:%d:%d:%d:%d:%.2f:%.2f:%d:%d:%d:%d:%d:%.2f:%d:%.2f:%d:%.2f:%d:%.2f:%d:%d:%d:%.2f:%d:%d:%d:%.2f:%.2f:%.2f:%.2f\n",
          time,
          $cpu->{total},  $ncpu,        $hcpu, 
          $cpu->{user},   $cpu->{nice}, $cpu->{system},  $cpu->{idle},
          $cpu->{iowait}, $cpu->{irq},  $cpu->{softirq}, $cpu->{steal},
          $rqsz,       $tcount,
          $mem->{memusedper},  $mem->{memused},
          $mem->{memfree},     $mem->{memtotal},
          $mem->{buffers},     $mem->{cached},
          $mem->{realfree},    $mem->{realfreeper},
          $mem->{swapusedper}, $mem->{swapused},
          $mem->{swapfree},    $mem->{swaptotal},
          $mem->{swapcached},

          $reads,  $rkbytes,
          $writes, $wkbytes,
          $iops,   $rwkbytes,

          $rxpkt, $rxkbytes, $rxerr, $rxdrop,
          $txpkt, $txkbytes, $txerr, $txdrop,
          $rxtxpkt, $rxtxkbytes,

          $la->{avg_1}, $la->{avg_5}, $la->{avg_15};


        close ($rawkrd);

    } else {

        printf
        "%.${tp}f:%.2f:%.2f:%.2f:%.2f:%.2f:%.2f:%.2f:%.2f:%.2f:%.2f:%.2f:%d:%d:%.2f:%d:%d:%d:%d:%d:%d:%.2f:%.2f:%d:%d:%d:%d:%d:%.2f:%d:%.2f:%d:%.2f:%d:%.2f:%d:%d:%d:%.2f:%d:%d:%d:%.2f:%.2f:%.2f:%.2f\n",
          time,
          $cpu->{total},  $ncpu,        $hcpu,
          $cpu->{user},   $cpu->{nice}, $cpu->{system},  $cpu->{idle},
          $cpu->{iowait}, $cpu->{irq},  $cpu->{softirq}, $cpu->{steal},
          $rqsz,       $tcount,
          $mem->{memusedper},  $mem->{memused},
          $mem->{memfree},     $mem->{memtotal},
          $mem->{buffers},     $mem->{cached},
          $mem->{realfree},    $mem->{realfreeper},
          $mem->{swapusedper}, $mem->{swapused},
          $mem->{swapfree},    $mem->{swaptotal},
          $mem->{swapcached},

          $reads,  $rkbytes,
          $writes, $wkbytes,
          $iops,   $rwkbytes,

          $rxpkt, $rxkbytes, $rxerr, $rxdrop,
          $txpkt, $txkbytes, $txerr, $txdrop,
          $rxtxpkt, $rxtxkbytes,

          $la->{avg_1}, $la->{avg_5}, $la->{avg_15};         

    }

    $lxs->init;

    ### Check for end
    last if ++$loop == $loop_max;

    ### Interval
    pause;

}



### SUBROUTINES


# open JSON configuration file
#
sub open_config {

    my ($conf) = @_;

    my $json_data;

    {
        local $/;

        # we will parse now the file
        if ( defined $ENV{'KRMX_PREFIX'} ) {
            if ( -e "$ENV{'KRMX_PREFIX'}/etc/$conf" ) {
                open my $fh, "<", "$ENV{'KRMX_PREFIX'}/etc/$conf";
                $json_data = <$fh>;
                close $fh;
            }
            else {
                print "error: open_conf - $! $ENV{'KRMX_PREFIX'}/etc/$conf \n";
                usage();
            }
        }
        else {
            if ( -e "/opt/kronometrix/etc/$conf" ) {
                open my $fh, "<", "/opt/kronometrix/etc/$conf";
                $json_data = <$fh>;
                close $fh;
            }
            else {
                print "error: open_conf - $! /opt/kronometrix/etc/$conf \n";
                usage();
            }
        }
    }

    my $perl_data = JSON->new->utf8->decode($json_data);

    return $perl_data;
}


# get log defintion
#
sub get_log {
    my ($data) = @_;

    my $bpath = $data->{'log'}->{'base_path'};
    my $cpath = $data->{'log'}->{'current_path'};

    return ( $bpath, $cpath );
}


# write_log - write log message
#
sub write_log {

    my ($logbuf) = @_;
    my ( $sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst ) =
    localtime(time);

    my $dt = sprintf "%4d-%02d-%02d %02d:%02d:%02d",
                $year + 1900, $mon + 1, $mday, $hour, $min, $sec;

    if (eof $log) {
        print $log "$dt $logbuf\n";
    } else {
        print $log "\n$dt $logbuf";
    }

    return;
}


# open_data - open data file
#
sub open_file {

    my ($file) = @_;
    my $fh;

    if (-f $file) {
        open $fh, "+>>", "$file" or 
	  die "error: open_file - cannot open $file $!";
	seek $fh, 0, 2;
        select ((select ($fh), $| = 1)[0]);

    } else {
        open $fh, "+>", "$file" or 
	  die "error: open_file - cannot open $file $!";
        select ((select ($fh), $| = 1)[0]);

    }

    return $fh;
}


# get_disks - return the attached disks on the system.
#
sub get_disks {

    my @d;

    # get disks
    opendir(my $dh, $sb) 
        or die "Error: Cannot find block directory: $!\n";

    while(readdir $dh) {
        # discard parent dir
        next if ($_ =~ m/^\./);

        # exceptions
        next if ($_ =~ m/^loop|^ram|^zram/);
        next if ($_ =~ m/^fd0|^hdc/);
        next if ($_ =~ m/^md(?:[0-9])/);
        next if ($_ =~ m/^sr(?:[0-9])$/);
        
        # print disks
        # print "Disk: $_\n";
        push @d, "$_";
    }
    closedir $dh;

    # return array ref
    return \@d;
}


sub get_part {
    my ($entry) = @_;
    my ($s, $maj, $min, $bks, $name);

    chomp $entry;
    return if $entry =~ /^major/;
    return if $entry =~ /^$/;
   
    # we return any valid partition number, we skip entire disk
    if ($entry =~ /(?:[0-9])$/) {
        ( $s, $maj, $min, $bks, $name ) = split /\s+/, $entry;
        return $name;
    }

    return;
}


# usage - print usage and exit.
#
sub usage {
    print STDERR <<END;
USAGE: sysrec [-hlV] | [interval [count]]
 e.g. sysrec 5       print continuously, every 5 seconds
      sysrec 1 5     print 5 times, every 1 second
      sysrec .5      print continuously, every 0.5 seconds
      sysrec -l 60   print continuously, every 60 seconds to raw datafile

 FIELDS:
  CPU
   #01 timestamp  : seconds since Epoch, time
   #02 cpupct     : cpu utilization, across all cpus, number
   #03 sumpct     : sum of all cpus utilization, number
   #04 headpct    : headroom cpu available, all cpus, number
   #05 userpct    : cpu utilization, user space in percent, number
   #06 nicepct    : cpu utilization, user space with nice priority, number
   #07 sysct      : cpu utilization, system space, number
   #08 idlepct    : cpu utilization, idle state, number
   #09 iowaitcpt  : cpu percentage in idle state because an I/O operation 
                     is waiting to complete, number
   #10 irqpct     : cpu percentage servicing interrupts, number
   #11 softirqpct : cpu percentage servicing softirqs, number
   #12 stealpct   : cpu percentage of time spent in other operating systems
                    when running in a virtualized environment, number
   #13 runqsz     : run queue length, tasks waiting for run time, number
   #14 plistsz    : tasks in the task list, number

  MEM
   #15 memusedpct : size of used memory in percent, number
   #16 memused    : size of used memory in kilobytes, number
   #17 memfree    : size of free memory in kilobytes, number
   #18 memtotal   : size of memory in kilobytes, number
   #19 buffers    : size of buffers used from memory in kilobytes, number
   #20 cached     : size of cached memory in kilobytes, number
   #21 realfree   : size of memory is real free, number
                     (memfree+buffers+cached)
   #22 realfreepct: size of memory real free in percent of total memory, number
   #23 swapusedpct: size of used swap space in percent, number
   #24 swapused   : size of swap space is used is kilobytes, number
   #25 swapfree   : size of swap space is free in kilobytes, number
   #26 swaptotal  : size of swap space in kilobytes, number
   #27 swapcached : memory that once was swapped out, is swapped back in 
                     but still also is in the swapfile, number

  DISK
   #28 reads      : disk read requests per second, rate
   #29 rkbytes    : read KB per second, rate
   #30 writes     : disk write requests per second, rate
   #31 wkbytes    : write KB per second, rate
   #32 iops       : disk read+write requests per second, rate
   #33 rwkbytes   : read+write KB per second, rate

  NIC
   # rx received, inbound
   #34 rxpkt      : rx packets per sec, rate
   #35 rxkbytes   : rx KB per sec, rate
   #36 rxerr      : rx packets containing errors, rate
   #37 rxdrop     : number of rx packets that were dropped per second, rate
   
   # tx transmitted, outbound
   #38 txpkt      : tx packets per sec, rate
   #39 txkbytes   : tx KB per sec, rate
   #40 txerr      : tx packets containing errors, rate
   #41 txdrop     : number of tx packets that were dropped per second, rate

   # throughput
   #42 rxtxpkt    : rx+tx packets per sec, rate
   #43 rxtxkbytes : rx+tx KB per sec, rate
 
   #44 avg1       : LA of the last minute, number
   #45 avg5       : LA of the last 5 minutes, number
   #46 avg15      : LA of the last 15 minutes, number

END
    exit 0;
}


# revision - print revision and exit
#
sub revision {
    print STDERR <<END;
sysrec: 1.1.5, 2019-10-24 2211
END
    exit 0;
}
